setpatch QUIET
version >NIL:

assign >NIL: exists DF0:
if not warn
  addbuffers >NIL: DF0: 15
endif

failat 21

makedir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
copy >NIL: ENVARC: RAM:ENV ALL NOREQ

resident >NIL: C:assign PURE
resident >NIL: C:execute PURE

assign >NIL: ENV: RAM:ENV
assign >NIL: T: RAM:T
assign >NIL: CLIPS: RAM:Clipboards
assign >NIL: REXX: S:
assign >NIL: PRINTERS: DEVS:Printers
assign >NIL: KEYMAPS: DEVS:Keymaps
assign >NIL: LOCALE: SYS:Locale
assign >NIL: LIBS: SYS:Classes ADD
assign >NIL: HELP: LOCALE:Help DEFER

binddrivers
mount >NIL: DEVS:DOSDrivers/~(#?.info)

DEVS:Monitors/PAL
DEVS:Monitors/NTSC

setenv Language "english"
setenv Workbench $Workbench
setenv Kickstart $Kickstart
unset Workbench
unset Kickstart

adddatatypes REFRESH QUIET
loadresource LIBS:diskfont.library LIBS:iffparse.library LIBS:asl.library LIBS:commodities.library
iprefs
conclip

path >NIL: RAM: C: SYS:Utilities SYS:Rexxc SYS:System S: SYS:Prefs SYS:WBStartup SYS:Tools SYS:Tools/Commodities
copy >NIL: SYS:Prefs/Icons/RAM/Disk.info RAM:

bblank
alias clear "echo *"*E[0;0H*E[J*" "

;Mount shared folder
mount-misterfs
ags-notify

;Launch AGS
assign >NIL: exists Saves:
if not warn
  if exists Saves:WHDSaves
    assign WHDSaves: Saves:WHDSaves
  endif
endif

assign >NIL: exists Data:
if not warn
  if exists Amiga:AGS2
    if exists Data:WHD
      assign AGS: Amiga:AGS2
      assign WHD0: Data:WHD
      assign >NIL: exists WHDSaves:
      if warn
        echo ""
        echo ">>> Using MegaAGS.hdf for save data <<<"
        echo ""
        echo "Mount MegaAGS-Saves.hdf as secondary drive to enable a"
        echo "save disk that can easily be carried over when upgrading"
        echo "to new releases of MegaAGS."
        wait 4
        assign WHDSaves: SYS:WHDSaves
      endif
      ags-vadjust
      if exists MiSTer:ags_boot
        cpr MiSTer:ags_boot T:ags_boot MOVE CLONE SILENT
        pickfile f T:ags_boot q c ags-launch ""[]"
      endif
      AGS:AGS2
    endif
  endif
endif

;Continue to WB

;KingCON
assign CON: DISMOUNT
assign RAW: DISMOUNT
mount CON: from DEVS:KingCON-mountlist
mount RAW: from DEVS:KingCON-mountlist

;RTG support
if `ags-uname` EQ "mister"
  assign Picasso96: SYS:Tools/Picasso96
  DEVS:Monitors/MiSTer
endif

assign INCLUDE: LIBS:include
assign APPS: SYS:Apps
assign Dev: APPS:Dev

assign CSH: SYS:Tools/csh
assign vim: SYS:Tools/vim
assign home: S:
path vim:

ec-select

assign asmpro: Dev:AsmPro
assign Blitz2: Dev:Blitz
assign blitzlibs: Blitz2:blitzlibs

assign C4D: APPS:Graphics/Cinema4D
assign TEX: C4D:Textures
assign Toaster: APPS:Graphics/Toaster
assign 3D: Toaster:3d
assign DPaintV: APPS:Graphics/DPaintV
assign Mline: APPS:Audio/Mline
assign "MT-32 Editor:" APPS:Audio/MT32Editor
assign XORFiles: APPS:Audio/X-oR

assign ST-00: APPS:Audio/ProTracker2
assign >NIL: exists Data:
if not warn
  if exists Data:Music
    assign Music: Data:Music
    if exists Music:Samples
      assign ST-01: Music:Samples/ST-01
      assign ST-02: Music:Samples/ST-02
      assign ST-03: Music:Samples/ST-03
    endif
  endif
endif

resident assign REMOVE
resident execute REMOVE

;Brilliance
setpatchmrgcop >NIL:

;Custom startup file that survives upgrades of main image
assign >NIL: exists Saves:
if not warn
  if exists Saves:custom-startup
    execute Saves:custom-startup
  endif
endif

loadwb DELAY
if exists S:wb-startup
  run >NIL: S:wb-startup
endif
endcli
